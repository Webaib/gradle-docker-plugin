buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

apply plugin: 'maven-publish'
apply plugin: com.jfrog.bintray.gradle.BintrayPlugin

publishing {
    publications {
        mavenJava(MavenPublication) {
			groupId 'net.overscale'
			
            from components.java
            artifact sourcesJar
            artifact groovydocJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle Docker plugin')
                root.appendNode('description', 'Gradle plugin for managing Docker images and containers.')
                root.appendNode('url', 'https://github.com/bmuschko/gradle-docker-plugin')
                root.appendNode('inceptionYear', '2015')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/Webaib/gradle-docker-plugin')
                scm.appendNode('connection', 'scm:https://webaib@github.com/Webaib/gradle-docker-plugin.git')
                scm.appendNode('developerConnection', 'scm:git://github.com/Webaib/gradle-docker-plugin.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def bmuschko = developers.appendNode('developer')
                bmuschko.appendNode('id', 'Webaib')
                bmuschko.appendNode('name', 'Juri Pavlov')
                bmuschko.appendNode('email', 'webaib@gmail.com')
            }
        }
    }
	
	repositories {
		maven() {
			name = "mySnapshot"
			credentials {
				username = "admin"
				password = resolveProperty('ARTIFACTORY_PAS', 'artifactoryPas')
			}
			url "http://10.0.3.166:9080/artifactory/plugins-snapshot-local/"
		}
		
	}
}

bintray {
    user = resolveProperty('BINTRAY_USER', 'bintrayUser')
    key = resolveProperty('BINTRAY_KEY', 'bintrayKey')
    publications = ['mavenJava']

    pkg {
        repo = 'gradle-plugins'
        name = 'com.bmuschko:gradle-docker-plugin'
        desc = 'Gradle plugin for managing Docker images and containers.'
        websiteUrl = "https://github.com/bmuschko/${project.name}"
        issueTrackerUrl = "https://github.com/bmuschko/${project.name}/issues"
        vcsUrl = "https://github.com/bmuschko/${project.name}.git"
        licenses = ['Apache-2.0']
        labels = ['gradle', 'docker', 'container', 'image', 'lightweight', 'vm', 'linux']
        publicDownloadNumbers = true

        version {
            vcsTag = "v$project.version"
            attributes = ['gradle-plugin': ["com.bmuschko.docker-remote-api:${project.group}:${project.name}",
                                            "com.bmuschko.docker-java-application:${project.group}:${project.name}"]]

            gpg {
                sign = true
                passphrase = resolveProperty('GPG_PASSPHRASE', 'gpgPassphrase')
            }
        }
    }
}

String resolveProperty(String envVarKey, String projectPropKey) {
    String propValue = System.getenv()[envVarKey]
	

    if(propValue) {
        return propValue
    }

    hasProperty(projectPropKey) ? getProperty(projectPropKey) : null
}